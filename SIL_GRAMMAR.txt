SIL Grammar
Maximilian Hasselbusch
Last Updated: 16 January 2016

**For use with the SILC program**

**The grammar was originally written by Dr. James Femister, this document has been recreated and provided by Maximilian Hasselbusch only as a reference**

<…text…>: nonterminal
ALL CAPS TEXT: terminal/keyword

Grammar:

<Program> -> PROGRAM IDENT <CompoundStatement> END PROGRAM
<CompoundStatement> -> <Statement> {<Statement>}
<Statement> -> (<SetStatement>|<PrintStatement>|<WhileStatement>|<IfStatement>)
<SetStatement> -> SET IDENT ASSIGN <Expression>
<PrintStatement> -> PRINT <PrintExpression> {COMMA <PrintExpression>}
<WhileStatement> -> WHILE <LogicalExpression> DO <CompoundStatement> END
<IfStatement> -> IF <LogicalExpression> THEN <CompoundStatement> [ELSE <CompoundStatement>] ENDIF
<PrintExpression> -> (<Expression> | STRLIT)
<LogicalExpression> -> <RelationalExpression> {(AND | OR) <RelationalExpression>}
<RelationalExpression> -> <Expression> (EQ | LT | LE | GT | GE | NE) <Expression>
<Expression> -> <Term> {(PLUS | MINUS) <Term>}
<Term> -> <Factor> {(TIMES | DIVIDE | REM) <Factor>}
<Factor> -> (LPAREN <Expression> RPAREN | INTLIT | FLOATLIT | IDENT)
